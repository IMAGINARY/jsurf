group = 'de.mfo.jsurf'
version = '0.4.1'

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'antlr'

targetCompatibility = 1.7
sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

// workaround for excluding certain ANTLR dependencies
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
    antlr group: "org.antlr", name: "antlr", version: "3.4"
    compile group: 'javax.vecmath', name: 'vecmath', version: '1.5.2'
    compile 'org.antlr:antlr-runtime:3.4@jar'
    compile 'commons-cli:commons-cli:1.2'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.12'
}

task sourceJar(type: Jar) {
        from sourceSets.main.allJava
}

jar {
    manifest {
        attributes( "Implementation-Title": project.group,
                   "Implementation-Version": project.version,
                   "Implementation-Vendor": "Mathematisches Forschungsinstitut Oberwolfach" )
    }
}

publishing {
        publications {
                mavenJava(MavenPublication) {
                        from components.java

                        artifact sourceJar {
                                classifier "sources"
                        }
                }
        }
        repositories {
                maven {
                        // define in local gradle.properties file
                        url project.properties[ 'publishing.repositories.maven.url' ]
                }
        }
}

mainClassName = "de.mfo.jsurf.Main"

run {
	args = [ "-s", "512", "--gui", "examples/record_kummerquartic.jsurf" ]
}
